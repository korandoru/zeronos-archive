plugins {
    id 'idea'
    id 'java'
    id 'java-library'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'com.diffplug.spotless' version '6.7.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.google.protobuf' version '0.8.18'
    id 'nebula.dependency-recommender' version '9.0.2'
}

dependencyRecommendations {
    propertiesFile file: file('versions.props')
}

allprojects {
    group 'io.korandoru.dryad'
    version '0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'nebula.dependency-recommender'

    sourceCompatibility = 17
    targetCompatibility = 17

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        implementation 'org.slf4j:slf4j-api'
        implementation 'org.slf4j:slf4j-simple'
    }
}

project('dryad-proto') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        implementation 'com.google.protobuf:protobuf-java'
        implementation 'org.apache.ratis:ratis-thirdparty-misc'
        protobuf files("proto/")
    }

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc'
        }
    }

    shadowJar {
        configurations = []
        relocate 'com.google.protobuf', 'org.apache.ratis.thirdparty.com.google.protobuf'
    }
}

project('dryad-server') {
    dependencies {
        implementation project(path: ':dryad-proto', configuration: 'shadow')
        implementation 'org.apache.ratis:ratis-server'
        implementation 'org.apache.ratis:ratis-grpc'
    }
}

project('dryad-client') {
    dependencies {
        implementation project(path: ':dryad-proto', configuration: 'shadow')
        implementation 'org.apache.ratis:ratis-server'
        implementation 'org.apache.ratis:ratis-grpc'
    }
}
